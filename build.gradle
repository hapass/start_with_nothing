ext {
    binPath = "bin"
    layoutPath = "layout"
    dataPath = "data"
    shadersPath = "src/main/glsl/engine/graphics/rendering"
    hxmlFile = "build.hxml"
    shaderToken = "<!-- INSERT_SHADERS -->"
    levelToken = "<!-- INSERT_LEVELS -->"
    emptyString = ""
}

//Build.

task compile(type: Exec) {
    commandLine "haxe", hxmlFile
}

task copyLayout(type: Copy) {
    from layoutPath
    into binPath
}

task embedShaders << {
    FileTree tree = fileTree(dir: shadersPath)
    tree.include "**/*.glsl"

    def tags = []

    tree.each { File file ->
        tags << "<script type=\"" << "text/glsl\"" << " id=\"" << file.name.replace(".glsl", "") << "\"" << " >" << "\n" << file.text << "\n" << "</script>" << "\n"
    }

    def file = new File(binPath + "/index.html")
    file.write file.text.replace(shaderToken, tags.join(emptyString))
}

task embedLevels << {
    FileTree tree = fileTree(dir: dataPath)
    tree.include "**/*.lvl"

    def tags = []

    tree.each { File file ->
        tags << "<div" << " id=\"" << file.name.replace(".lvl", "") << "\"" << " data-lvl-description=\"" << file.text.replace(" ", "").replace("\n", "") << "\"" << " />" << "\n"
    }

    def file = new File(binPath + "/index.html")
    file.write file.text.replace(levelToken, tags.join(emptyString))
}

embedShaders.mustRunAfter copyLayout
embedLevels.mustRunAfter copyLayout

task build << {
    println "Building..."
}

build.dependsOn compile
build.dependsOn copyLayout
build.dependsOn embedShaders
build.dependsOn embedLevels

//Run.

task startServer(type: Exec) {
    workingDir binPath
    commandLine "screen", "-S", "glow_server", "-d", "-m", "python", "-m", "SimpleHTTPServer", "8000"
}

task killServer(type: Exec) {
    commandLine "screen", "-S", "glow_server", "-X", "quit"
}

task run << {
   java.awt.Desktop.desktop.browse "http://localhost:8000".toURI()
}

